(in-package :aoc-2022)

(defun parse-file ()
  (parse-lines (with-monad
                 (assign dir (parse-keyword))
                 (parse-space)
                 (assign amount (parse-number))
                 (unit (list dir amount)))))

(defun move (pos dir)
  (destructuring-bind (r c) pos
    (ecase dir
      (:U (list (1- r) c))
      (:D (list (1+ r) c))
      (:L (list r (1- c)))
      (:r (list r (1+ c))))))

(defun update-tail (head tail)
  (let ((diff (map 'list #'- head tail)))
    (cond
      ((every (lambda (d) (<= (abs d) 1)) diff) tail)
      (t (map 'list #'+ tail
              (mapcar (lambda (d)
                        (if (= 0 d) 0 (/ d (abs d))))
                      diff))))))

(defun print-grid (head tail)
  (format nil "狺アㄩ翦ㄦ矧骝镯麸癌ㄣ镬戾泗ㄦ矧磲铋狺アㄩ翦ㄦ矧骝镯麸旦ㄣ镬戾泗ㄣ镱è羼踽扉篝悌桢徜＼醛è羼踽扉篝悌翎殪＼冤è羼踽扉篝悌Ж癌＼螬＼┅┅┅┅ㄤ彐躅溽ㄩ铕豸脲疳螋暴戾è铛憝腩雉ㄩ疳螋暴卑┅盹鲥蝓瞽疳蝮弪疳蝮瀛骈戾轭瘐舂┅ㄩ翦鏖翳腩雉ㄩ翦蝈疱狒铛憝腩雉螬ㄣ镬戾泗Ж癌┅鏖翳翎殪痫箝糸镱磲脲栳箬翎忪呼弩у聃犰┅ㄦ矧ㄤ轵邈糸镱犴秕铘轭盹鲥螬ㄩ翦蝈疱狒犴秕铘箦翩ㄥ祠腩雉癌盹鲥ㄥ祠腩雉癌溟蝈泗轱瞟ㄩ翦ㄦ矧骝镯忮祜铛憝腩雉螬箦翩ㄥ祠腩雉椹躔溽翦翎殪ㄥ祠腩雉ū椹ㄥ祠腩雉椹┅箦翩ㄧ弭栳箬ㄥ祠腩雉ū铛憝腩雉螬翎殪痫箝糸镱螬舂ㄦ轭犰禊蝈趱蝾ㄨ狍璀翎忪瀛泔躅翎殪痫箝糸镱螬┅┅