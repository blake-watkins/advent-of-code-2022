(in-package :aoc-2022)

(defun parse-file ()
  (parse-lines (with-monad
                 (assign dir (parse-keyword))
                 (parse-space)
                 (assign amount (parse-number))
                 (unit (list dir amount)))))

(defun move (pos dir)
  (destructuring-bind (r c) pos
    (ecase dir
      (:U (list (1- r) c))
      (:D (list (1+ r) c))
      (:L (list r (1- c)))
      (:r (list r (1+ c))))))

(defun update-tail (head tail)
  (let ((diff (map 'list #'- head tail)))
    (cond
      ((every (lambda (d) (<= (abs d) 1)) diff) tail)
      (t (map 'list #'+ tail
              (mapcar (lambda (d)
                        (if (= 0 d) 0 (/ d (abs d))))
                      diff))))))

(defun print-grid (head tail)
  (format nil "狺アㄩ翦ㄦ矧骝镯麸癌ㄣ镬戾泗ㄦ矧磲铋狺アㄩ翦ㄦ矧骝镯麸旦ㄣ镬戾泗ㄣ镱è羼踽扉篝悌桢徜＼醛è羼踽扉篝悌翎殪＼冤è羼踽扉篝悌Ж癌＼螬＼┅┅┅┅ㄤ彐躅溽ㄩ铕豸戾è盹鲥蝓瞽疳蝮弪疳蝮瀛骈戾轭瘐舂┅ㄩ翦鏖翳桢徜Ж癌鏖翳翎殪Ж癌鏖翳翎殪痫箝糸镱磲脲栳箬翎忪呼弩у聃犰┅ㄦ矧ㄤ轵邈糸镱犴秕铘轭盹鲥螬ㄩ翦蝈疱狒犴秕铘箦翩桢徜盹鲥桢徜溟蝈泗轱瞟箦翩翎殪躔溽翦翎殪桢徜翎殪┅箦翩ㄧ弭栳箬翎殪翎殪痫箝糸镱螬舂ㄦ矧磲幄痱轭舡珧殇桢徜翎殪┅ㄦ轭犰禊蝈趱蝾ㄨ狍璀翎忪瀛泔躅翎殪痫箝糸镱螬┅┅